# SPDX-License-Identifier: Apache-2.0
# Copyright Open Network Fabric Authors
{
  sources,
  sanitizers,
  platform,
  profile,
  ...
}:
final: prev:
let
  dep =
    pkg:
    (pkg.override { stdenv = final.stdenv'; }).overrideAttrs (orig: {
      nativeBuildInputs = (orig.nativeBuildInputs or [ ]) ++ [ prev.removeReferencesTo ];
      postInstall = (orig.postInstall or "") + ''
        find "$out" \
            -type f \
            -exec remove-references-to -t ${final.stdenv'.cc} '{}' +;
        if [ ! -z "$lib" ] && [ -d "$lib"]; then
            find "$lib" \
                -type f \
                -exec remove-references-to -t ${final.stdenv'.cc} '{}' +;
        fi
      '';
    });
  frr-build =
    frrSrc:
    dep (
      (final.callPackage ../pkgs/frr (
        final.fancy
        // {
          stdenv = final.stdenv';
          inherit frrSrc;
        }
      )).overrideAttrs
        (orig: {
          LDFLAGS =
            (orig.LDFLAGS or "")
            + " -L${final.fancy.readline}/lib -lreadline "
            + " -L${final.fancy.json_c}/lib -ljson-c "
            + " -Wl,--push-state,--as-needed,--no-whole-archive,-Bstatic "
            + " -L${final.fancy.libxcrypt}/lib -lcrypt "
            + " -L${final.fancy.pcre2}/lib -lpcre2-8 "
            + " -L${final.fancy.xxHash}/lib -lxxhash "
            + " -L${final.fancy.libgccjit}/lib -latomic "
            + " -Wl,--pop-state";
          configureFlags = orig.configureFlags ++ [
            "--enable-shared"
            "--enable-static"
            "--disable-static-bin"
          ];
          nativeBuildInputs = (orig.nativeBuildInputs or [ ]) ++ [ prev.nukeReferences ];
          # disallowedReferences = (orig.disallowedReferences or []) ++ [ final.stdenv'.cc ];
          preFixup = ''
            find "$out" \
                -type f \
                -exec nuke-refs \
                -e "$out" \
                -e ${final.stdenv'.cc.libc} \
                -e ${final.python3Minimal} \
                -e ${final.fancy.readline} \
                -e ${final.fancy.libgccjit} \
                -e ${final.fancy.json_c} \
                '{}' +;
          '';
        })
    );
in
{
  fancy = prev.fancy // {
    inherit sources;
    xxHash = (dep prev.xxHash).overrideAttrs (orig: {
      cmakeFlags = (orig.cmakeFlags or [ ]) ++ [
        "-DBUILD_SHARED_LIBS=OFF"
        "-DXXH_STATIC_LINKING_ONLY=ON"
      ];
    });
    libyang = (
      (prev.libyang.override {
        stdenv = final.stdenv';
        pcre2 = final.fancy.pcre2;
        xxHash = final.fancy.xxHash;
      }).overrideAttrs
        (orig: {
          cmakeFlags = (orig.cmakeFlags or [ ]) ++ [ "-DBUILD_SHARED_LIBS=OFF" ];
          propagatedBuildInputs = [
            final.fancy.pcre2
            final.fancy.xxHash
          ];
        })
    );
    libcap = (
      (prev.libcap.override {
        stdenv = final.stdenv';
        usePam = false;
        withGo = false;
      }).overrideAttrs
        (orig: {
          doCheck = false; # tests require privileges
          separateDebugInfo = false;
          CFLAGS = "-ffat-lto-objects -fsplit-lto-unit";
          makeFlags = [
            "lib=lib"
            "PAM_CAP=no"
            "CC:=clang"
            "SHARED=no"
            "LIBCSTATIC=no"
            "GOLANG=no"
          ];
          configureFlags = (orig.configureFlags or [ ]) ++ [ "--enable-static" ];
          postInstall = orig.postInstall + ''
            # extant postInstall removes .a files for no reason
            cp ./libcap/*.a $lib/lib;
          '';
        })
    );
    json_c = dep (
      (dep prev.json_c).overrideAttrs (orig: {
        cmakeFlags = (orig.cmakeFlags or [ ]) ++ [
          "-DENABLE_STATIC=1"
        ];
        postInstall = (orig.postInstall or "") + ''
          mkdir -p $dev/lib
          $RANLIB libjson-c.a;
          cp libjson-c.a $out/lib;
          find "$out" \
              -type f \
              -exec remove-references-to -t ${final.stdenv'.cc} '{}' +;
        '';
        nativeBuildInputs = (orig.nativeBuildInputs or [ ]) ++ [ prev.removeReferencesTo ];
        disallowedReferences = (orig.disallowedReferences or [ ]) ++ [ final.stdenv'.cc ];
      })
    );
    rtrlib = dep (
      prev.rtrlib.overrideAttrs (orig: {
        cmakeFlags = (orig.cmakeFlags or [ ]) ++ [ "-DENABLE_STATIC=1" ];
      })
    );
    abseil-cpp = dep prev.abseil-cpp;
    zlib = (
      prev.zlib.override {
        stdenv = final.stdenv';
        static = true;
        shared = false;
      }
    );
    pcre2 = dep (
      prev.pcre2.overrideAttrs (orig: {
        configureFlags = (orig.configureFlags or [ ]) ++ [
          "--enable-static"
          "--disable-shared"
        ];
      })
    );
    ncurses = dep (
      prev.ncurses.override {
        stdenv = final.stdenv';
        enableStatic = true;
        withCxx = false;
      }
    );
    readline = dep (
      (prev.readline.override {
        stdenv = final.stdenv';
        ncurses = final.fancy.ncurses;
      }).overrideAttrs
        (orig: {
          nativeBuildInputs = (orig.nativeBuildInputs or [ ]) ++ [ prev.removeReferencesTo ];
          disallowedReferences = (orig.disallowedReferences or [ ]) ++ [ final.stdenv'.cc ];
          configureFlags = (orig.configureFlags or [ ]) ++ [
            "--enable-static"
            "--enable-shared"
          ];
          postInstall = (orig.postInstall or "") + ''
            find "$out" \
                -type f \
                -exec remove-references-to -t ${final.stdenv'.cc} '{}' +;
          '';
        })
    );
    libxcrypt = (dep prev.libxcrypt).overrideAttrs (orig: {
      configureFlags = (orig.configureFlags or [ ]) ++ [
        "--enable-static"
        "--disable-shared"
      ];
    });
    libgccjit =
      (prev.libgccjit.override {
        # TODO: debug issue preventing clang build
        # stdenv = final.stdenv';
        libxcrypt = final.fancy.libxcrypt;
      }).overrideAttrs
        (orig: {
          configureFlags = (orig.configureFlags or [ ]) ++ [
            "--disable-static"
            "--enable-shared"
          ];
        });
    c-ares = dep (
      prev.c-ares.overrideAttrs (orig: {
        cmakeFlags = (orig.cmakeFlags or [ ]) ++ [
          "-DCARES_SHARED=OFF"
          "-DCARES_STATIC=ON"
        ];
      })
    );
    frr-agent = dep (final.callPackage ../pkgs/frr-agent final.fancy);
    frr-config = dep (final.callPackage ../pkgs/frr-config final.fancy);
    dplane-rpc = dep (final.callPackage ../pkgs/dplane-rpc final.fancy);
    dplane-plugin = dep (final.callPackage ../pkgs/dplane-plugin final.fancy);
    frr.host = frr-build sources.frr;
    frr.dataplane = frr-build sources.frr-dp;
  };
}
